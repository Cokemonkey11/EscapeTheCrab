package Units
import UnitObjEditing
import LinkedListModule
import HighelvesIds


public class Unit
	use LinkedListModule
	UnitDefinition def
	
	int id
	int hp = 10
	int movespeed = 300
	int damage = 2
	real cooldown = 0.5
	int goldcost = 0
	int lumbercost = 5
	int buildtime = 5
	real scale = 1.
	string modelpath = "default"
	string icon = "default"
	string name
	
	construct(int origId, int newId, string name)
		def = new UnitDefinition(newId, origId)
		this.name = name
		this.id = newId
		
		
	function generate()
		def..setHitPointsMaximumBase(hp)
		..setSpeedBase(movespeed)
		..setName(name)
		..setAttack1DamageBase(damage)
		..setAttack1CooldownTime(cooldown)
		..setGoldCost(goldcost)
		..setLumberCost(lumbercost)
		..setBuildTime(buildtime)
		..setScalingValue(scale)
		..setAttack1TargetsAllowed("enemy")
		..setTooltipBasic("Train " + name)
		..setTooltipExtended("Very good against crabs, or nagas.")

		if modelpath != "default"
			def.setModelFile(modelpath)
		if icon != "default"
			def.setIconGameInterface("ReplaceableTextures\\CommandButtons\\" + icon)


public function initHEUnits(boolean c)
	var b = new Unit('nbee', WORKER_ID, "Worker")
	b.def.setStructuresBuilt(idInteger2IdString(HOUSE_ID) + "," + idInteger2IdString(BASIC_WALL_ID) + "," + idInteger2IdString(BASIC_TOWER_ID) + "," + idInteger2IdString(BARRACKS_ID))
	b.modelpath = "units\\critters\\HighElfPeasant\\HighElfPeasant.mdl"
	b.icon = "BTNBloodElfPeasant.blp"
	b.hp = 100
	
	b = new Unit('nhea', ARCHER_ID, "Archer")
	b.damage = 15
	b.hp = 20
	b.cooldown = 0.45
	b.modelpath = "units\\creeps\\HighElfArcher\\HighElfArcher"
	b.icon = "BTNHighElvenArcher.blp"
	
	b = new Unit('hfoo', FOOTMAN_ID, "Footman")
	b.damage = 10
	b.hp = 50
	b.cooldown = 0.45
	b.modelpath = "units\\human\\TheCaptain\\TheCaptain.mdl"
	b.icon = "BTNTheCaptain.blp"
	
	b = new Unit('hbot', BOAT_ID, "BOAT")
	b.scale = 3.
	b.hp = 1000
	
	if c
		var e = Unit.first
		while e != null
			e.generate()
			e = e.next
	
	